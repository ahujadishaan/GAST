ext.versions = [
        androidGradlePlugin: '3.6.0',
        compileSdk         : 29,
        minSdk             : 21,
        targetSdk          : 29,
        buildTools         : '29.0.1',
        kotlinVersion      : '1.3.70',

]

ext.libraries = [
        androidGradlePlugin: "com.android.tools.build:gradle:$versions.androidGradlePlugin"
]

ext.getExportPackageName = { ->
    // Retrieve the app id from the project property set by the Godot build command.
    String appId = project.hasProperty("export_package_name") ? project.property("export_package_name") : ""
    // Check if the app id is valid, otherwise use the default.
    if (appId == null || appId.isEmpty()) {
        appId = "fhuya.godot.samples.gast.vr.video"
    }
    return appId
}

/**
 * Parse the project properties for the 'custom_template_v2_plugins' property and return
 * their binaries for inclusion in the build dependencies.
 *
 * The listed plugins must have their binaries in the 'libs/plugins' directory.
 */
ext.getGodotPluginsBinaries = { ->
    // Default dependencies
    String[] binDeps = ["GodotPaymentV3*.aar"]

    // Retrieve the list of enabled v2 plugins.
    String pluginsList = project.hasProperty("custom_template_v2_plugins") ? project.property("custom_template_v2_plugins") : ""
    if (pluginsList != null && !pluginsList.trim().isEmpty()) {
        for (String plugin : pluginsList.split(",")) {
            binDeps += plugin + "*.aar"
        }
    }

    return binDeps
}
